#!/bin/csh
cd /disk3/data/fcst
set path=(~/bin $path)
rehash

setenv DAY `date -u +%y%m%d`
setenv YESTERDAY `ystrd`
setenv DD `date +%d`
setenv YD `yday`
# It is assumed that the timezone is west of GMT...

#foreach city (`cat new.target.list`)
foreach city (`cat grid.list`)

# Station Identification
	set L_CITY=`echo ${city} | awk -f ~/bin/fourletter.awk`
	echo STATION=${L_CITY}

# 12Z cloudiness
	rm -f $$.tmp
	foreach hour (7 8 9 10 11 12)
		grep ^$city raw/cloud.${hour} | cut -f2 - >> $$.tmp
	end
	grep -F 0.75 < $$.tmp | wc -l > $$.t1
	grep -F 1.00 < $$.tmp | wc -l > $$.t2
	if (`cat $$.t1` + `cat $$.t2` >= 3) then
		echo CLOUD_12=1
	else
		echo CLOUD_12=2
	endif
	rm -f $$.tmp $$.t1 $$.t2

# 18Z cloudiness
	rm -f $$.tmp
	foreach hour (15 16 17 18)
		grep ^$city raw/cloud.${hour} | cut -f2 - >> $$.tmp
	end
	grep -F 0.75 < $$.tmp | wc -l > $$.t1
	grep -F 1.00 < $$.tmp | wc -l > $$.t2
	if (`cat $$.t1` + `cat $$.t2` >= 2) then
		echo CLOUD_18=1
	else
		echo CLOUD_18=2
	endif
	rm -f $$.tmp $$.t1 $$.t2

# 12Z advection
	echo a = `grep ${L_CITY} raw/af11.list | cut -f2 -` + \
		`grep ${L_CITY} raw/af12.list | cut -f2 -` > $$.tmp
	echo ADVECT_12=`cat $$.tmp src/advect.bc | bc`
	rm -f $$.tmp

# 18Z advection
	echo a = `grep ${L_CITY} raw/af17.list | cut -f2 -` + \
		`grep ${L_CITY} raw/af18.list | cut -f2 -` > $$.tmp
	echo ADVECT_18=`cat $$.tmp src/advect.bc | bc`
	rm -f $$.tmp

# 12Z fronts
	set name={WARM,COLD,OCFNT,STNRY}
	set FRONT
	foreach type (1 2 3 4)
		unset foo
		foreach hour (10 13)
			set FILE=raw/${YESTERDAY}${hour}.${name[${type}]}
			set RANGE=`grep ${L_CITY} $FILE | cut -f2 -`
			if ("${RANGE}" == close) then
				set foo=${type}
			endif
		end
		if (${?foo} == 1) then
			set FRONT=${FRONT}\|${foo}
		endif
	end
	if ("${FRONT}" == "") then
		echo FRONT_12=5
	else
		echo FRONT_12=`echo ${FRONT} | sed -e '1,$s/^|//g'`
	endif
	unset foo
	unset FRONT
	unset name

# 18Z fronts
	set name={WARM,COLD,OCFNT,STNRY}
	set FRONT
	foreach type (1 2 3 4)
		foreach hour (16 19)
			unset foo
			set FILE=raw/${YESTERDAY}${hour}.${name[${type}]}
			set RANGE=`grep ${L_CITY} $FILE | cut -f2 -`
			if ("${RANGE}" == close) then
				set foo=${type}
			endif
		end
		if (${?foo} == 1) then
			set FRONT=${FRONT}\|${foo}
		endif
	end
	if ("${FRONT}" == "") then
		echo FRONT_18=5
	else
		echo FRONT_18=`echo ${FRONT} | sed -e '1,$s/^|//g'`
	endif
	unset foo
	unset FRONT
	unset name

#
# Precipitation Influences
#
	set FP

# moisture supply
	foreach TIME (${YD}_12 ${DD}_0 ${DD}_12)
		set RH=`grep ${L_CITY} raw/hf${TIME}.list | cut -f2 -`
		echo a = ${RH} > $$.tmp
		if ("${RH}" == "") then
		else
			echo "if (a >= 80) 1" >> $$.tmp
			echo "if (a < 80)  0" >> $$.tmp
			if (`bc < $$.tmp` == 1) then
				set FP=,1
			endif
			rm -f $$.tmp
		endif
	end
	unset RH

# frontal passage
	grep ${L_CITY} raw/${YESTERDAY}{1{2,3,4,5,6,7,8},2{0,1,2,3}}.* \
		raw/${DAY}{0*,10,11}.* | grep close > $$.tmp
	if (`wc -l < $$.tmp` > 0) then
		set FP=${FP},2
	endif
	rm -f $$.tmp

# unstable atmosphere
	set foo=`grep ${L_CITY} raw/tf0.list | cut -f2 -`
	set bar=`grep ${L_CITY} raw/tf12.list | cut -f2 -`
	if ("${foo:r}" >= 25) then
		set FP=${FP},3
	else if ("${bar:r}" >= 25) then
		set FP=${FP},3
	endif
	unset foo bar

# combine the precipitation influence answers
	if ("${FP}" == "") then
		echo FAVOR_PRECIP=0
	else
		echo FAVOR_PRECIP=`echo ${FP} | sed -e '1,$s/^,//g'`
	endif
	unset FP

# surface temperature
	foreach hour (00 06 12 18)
		set ST=`grep ^$city raw/surface.list | grep /${hour} | cut -f3 | awk -f ~/bin/spread.awk`
		echo TEMP_${hour}=${ST}
	end

# surface dewpoint
	foreach hour (00 06 12 18)
		set DP=`grep ^$city raw/surface.list | grep /${hour} | cut -f4 | awk -f ~/bin/spread.awk`
		echo DEWP_${hour}=${DP}
	end

# wind direction
	set WD=`grep ^$city raw/surface.list | grep /18 | cut -f5`
	echo WDIR_18=`echo ${WD} | awk -f ~/bin/ntodir.awk`

# wind speed
#	set SP=`grep ^$city raw/surface.list | grep /18 | cut -f6 | awk -f ~/bin/wspread.awk`
	set SP=`grep ^$city raw/surface.list | grep /18 | cut -f6 | awk -f ~/bin/spread.awk`
	echo WSPD_18=${SP}

# precipitation
	set PRECIP=`grep ^$city raw/precip.list | cut -f2 | head -1`
	if ${PRECIP} == -9999.00 then
		echo PRECIP=2
	else if ${PRECIP} == '' then
		echo PRECIP=2
	else
		echo PRECIP=1
	endif

end
